if (length(getvariable("main__", "")) == 0 or getvariable("main__", "") == currentscriptname) {
    ?currentscriptname;
        
    switchtodesign;
    groupscope("::Root Element");
    deleteall;
    clear;
    main__ = currentscriptname;

    lambda = 1550e-9;
    gap = 100e-9;
    pwr = 1e-3;
    N = 2;
    
    laser_sweep = false;
    record_all = true;
    annotate_all = 3;
}

# Adding Elements
reciprocal_ring_resonator;

if (laser_sweep) {
} else {
    multi_laser;
    select("multi_laser");
    copy();
    set("name", "laser");
    select("multi_laser");
    delete;
}

addelement("Straight Waveguide");
set("name", "W_L_T");
addelement("Straight Waveguide");
set("name", "W_L_B");
addelement("Straight Waveguide");
set("name", "W_R_T");
addelement("Straight Waveguide");
set("name", "W_R_B");
addelement("PIN Photodetector");
set("name", "PIN_T");
addelement("PIN Photodetector");
set("name", "PIN_TD");
addelement("PIN Photodetector");
set("name", "PIN_D");
eletrical_analysers_ports = {"PIN_T", "PIN_TD", "PIN_D"};
for (i = 1:length(eletrical_analysers_ports)) {
    addelement("Oscilloscope");
    set("name", "EOSC_" + eletrical_analysers_ports{i});
    addelement("Spectrum Analyzer");
    set("name", "ESA_" + eletrical_analysers_ports{i});
}
for (i = 1:N) {
    select("reciprocal_ring_resonator");
    copy();
    set("name", "R_" + num2str(i));
    if (i > 1) {
        addelement("Straight Waveguide");
        set("name", "W_T_" + num2str(i - 1) + "_" + num2str(i));
        addelement("Straight Waveguide");
        set("name", "W_B_" + num2str(i - 1) + "_" + num2str(i));
    }
}
select("reciprocal_ring_resonator");
delete;


# Positioning Elements
x_off = 0;
y_off = 0;
setnamed("W_L_T", "x position", x_off + 250);
setnamed("W_L_T", "y position", y_off - 75);
setnamed("W_L_B", "x position", x_off + 250);
setnamed("W_L_B", "y position", y_off + 75);
setnamed("W_R_T", "x position", x_off + (N - 1) * 400 + 650);
setnamed("W_R_T", "y position", y_off - 75);
setnamed("W_R_B", "x position", x_off + (N - 1) * 400 + 650);
setnamed("W_R_B", "y position", y_off + 75);
setnamed("PIN_T", "x position", x_off + (N - 1) * 400 + 900);
setnamed("PIN_T", "y position", y_off - 75);
setnamed("PIN_TD", "x position", x_off + (N - 1) * 400 + 900);
setnamed("PIN_TD", "y position", y_off + 75);
setnamed("PIN_D", "x position", x_off + (N - 1) * 400 + 900);
setnamed("PIN_D", "y position", y_off + 225);
for (i = 1:N) {
    setnamed("R_" + num2str(i), "x position", x_off + (i - 1) * 400 + 450);
    setnamed("R_" + num2str(i), "y position", y_off);
    if (i > 1) {
        setnamed("W_T_" + num2str(i - 1) + "_" + num2str(i), "x position", x_off + (i - 1) * 400 + 250);
        setnamed("W_T_" + num2str(i - 1) + "_" + num2str(i), "y position", y_off - 75);
        setnamed("W_B_" + num2str(i - 1) + "_" + num2str(i), "x position", x_off + (i - 1) * 400 + 250);
        setnamed("W_B_" + num2str(i - 1) + "_" + num2str(i), "y position", y_off + 75);
    }
}
for (i = 1:length(eletrical_analysers_ports)) {
    setnamed("EOSC_" + eletrical_analysers_ports{i}, "x position", getnamed(eletrical_analysers_ports{i}, "x position") + 200);
    setnamed("EOSC_" + eletrical_analysers_ports{i}, "y position", getnamed(eletrical_analysers_ports{i}, "y position") + 16);
    setnamed("ESA_" + eletrical_analysers_ports{i}, "x position", getnamed(eletrical_analysers_ports{i}, "x position") + 300);
    setnamed("ESA_" + eletrical_analysers_ports{i}, "y position", getnamed(eletrical_analysers_ports{i}, "y position") + 16);
}

# Adding Element Connections
connect("laser", "output", "W_L_T", "port 1");
connect("W_L_T", "port 2", "R_1", "lt_o");
connect("W_L_B", "port 2", "R_1", "lb_o");
connect("W_R_T", "port 1", "R_" + num2str(N), "rt_o");
connect("W_R_B", "port 1", "R_" + num2str(N), "rb_o");
connect("W_R_T", "port 2", "PIN_T", "input");
connect("W_L_B", "port 1", "PIN_D", "input");
connect("W_R_B", "port 2", "PIN_TD", "input");
for (i = 2:N) {
    connect("R_" + num2str(i - 1), "rt_o", "W_T_" + num2str(i - 1) + "_" + num2str(i), "port 1");
    connect("R_" + num2str(i - 1), "rb_o", "W_B_" + num2str(i - 1) + "_" + num2str(i), "port 1");
    connect("R_" + num2str(i), "lt_o", "W_T_" + num2str(i - 1) + "_" + num2str(i), "port 2");
    connect("R_" + num2str(i), "lb_o", "W_B_" + num2str(i - 1) + "_" + num2str(i), "port 2");
}
for (i = 1:length(eletrical_analysers_ports)) {
    connect(eletrical_analysers_ports{i}, "output", "EOSC_" + eletrical_analysers_ports{i}, "input");
    connect(eletrical_analysers_ports{i}, "output", "ESA_" + eletrical_analysers_ports{i}, "input");
}

# Array of Elements
elements = {"laser", "W_L_T", "W_L_B", "W_R_T", "W_R_B", "PIN_T", "PIN_TD", "PIN_D"};
R_elements = cell(N);
W_elements = cell((N - 1) * 2);
for (i = 1:N) { R_elements{i} = "R_" + num2str(i); }
for (i = 2:N) {
    W_elements{2 * (i - 2) + 1} = "W_T_" + num2str(i - 1) + "_" + num2str(i);
    W_elements{2 * (i - 2) + 2} = "W_B_" + num2str(i - 1) + "_" + num2str(i);
}
    
# Hiding Element Annotations
for (i = 1:length(elements)) { setnamed(elements{i}, "annotate", annotate_all >= 1); }
for (i = 1:length(R_elements)) { setnamed(R_elements{i}, "annotate", annotate_all >= 1); }
for (i = 1:length(W_elements)) { setnamed(W_elements{i}, "annotate", annotate_all >= 1); }

# For Recording/Degugging
if (record_all) {
    # Adding Analyzers
    optical_elements = {"laser", "W_L_T_1", "W_L_T_2", "W_L_B_1", "W_L_B_2", "W_R_T_1", "W_R_T_2", "W_R_B_1", "W_R_B_2"};
    
    optical_analysers_ports = cell(length(optical_elements) + (N * 4) + (N - 1) * 4);
    off = 0;
    for (i = 1:length(optical_elements)) {
        off = off + 1; optical_analysers_ports{off} = optical_elements{i}; 
    }
    for (i = 1:N) {
        off = off + 1; optical_analysers_ports{off} = "R_" + num2str(i) + "_lt";
        off = off + 1; optical_analysers_ports{off} = "R_" + num2str(i) + "_lb";
        off = off + 1; optical_analysers_ports{off} = "R_" + num2str(i) + "_rt";
        off = off + 1; optical_analysers_ports{off} = "R_" + num2str(i) + "_rb";
    }
    for (i = 2:N) {
        off = off + 1; optical_analysers_ports{off} = "W_T_" + num2str(i - 1) + "_" + num2str(i) + "_1";
        off = off + 1; optical_analysers_ports{off} = "W_T_" + num2str(i - 1) + "_" + num2str(i) + "_2";
        off = off + 1; optical_analysers_ports{off} = "W_B_" + num2str(i - 1) + "_" + num2str(i) + "_1";
        off = off + 1; optical_analysers_ports{off} = "W_B_" + num2str(i - 1) + "_" + num2str(i) + "_2";
    }
    
    for (i = 1:length(optical_analysers_ports)) {
        addelement("Optical Oscilloscope");
        set("name", "OOSC_" + optical_analysers_ports{i});
        addelement("Optical Spectrum Analyzer");
        set("name", "OSA_" + optical_analysers_ports{i});
        addelement("Optical Power Meter");
        set("name", "OPM_" + optical_analysers_ports{i});
    }

    # Adding Analyzers Connections
    for (i = 1:length(optical_analysers_ports)) {
        analyser = optical_analysers_ports{i};
        if (substring(analyser, 1, 2) == "R_") {
            element = substring(analyser, 1, length(analyser) - 3);
            element_port = substring(analyser, length(analyser) - 1, 2) + "_o";
            connect(element, element_port, "OOSC_" + analyser, "input");
            connect(element, element_port, "OSA_" + analyser, "input");
            connect(element, element_port, "OPM_" + analyser, "input");
        } else if (substring(analyser, 1, 2) == "W_") {
            element = substring(analyser, 1, length(analyser) - 2);
            element_port = "port " + substring(analyser, length(analyser), 1);
            connect(element, element_port, "OOSC_" + analyser, "input");
            connect(element, element_port, "OSA_" + analyser, "input");
            connect(element, element_port, "OPM_" + analyser, "input");
        } else {
            connect(analyser, "output", "OOSC_" + analyser, "input");
            connect(analyser, "output", "OSA_" + analyser, "input");
            connect(analyser, "output", "OPM_" + analyser, "input");
        }
    }
    
    
    # Positioning Elements
    fixed_up_element = {"laser", "W_L_T_1", "W_L_T_2", "W_R_T_1", "W_R_T_2"};    
    up_element = cell(length(fixed_up_element) + (N * 2) + (N - 1) * 2);
    off = 0;
    for (i = 1:length(fixed_up_element)) {
        off = off + 1; up_element{off} = fixed_up_element{i}; 
    }
    for (i = 1:N) {
        off = off + 1; up_element{off} = "R_" + num2str(i) + "_lt";
        off = off + 1; up_element{off} = "R_" + num2str(i) + "_rt";
    }
    for (i = 2:N) {
        off = off + 1; up_element{off} = "W_T_" + num2str(i - 1) + "_" + num2str(i) + "_1";
        off = off + 1; up_element{off} = "W_T_" + num2str(i - 1) + "_" + num2str(i) + "_2";
    }
    for (i = 1:length(up_element)) {
        analyser = up_element{i};
        y_off = 0;
        if (substring(analyser, 1, 2) == "R_") {
            element = substring(analyser, 1, length(analyser) - 3);
            if (substring(analyser, length(analyser) - 1, 1) == "l") {
                x_sign = -1.1;
            } else {
                x_sign = 1.8;
            }
            y_off = -75;
        } else if (substring(analyser, 1, 2) == "W_") {
            element = substring(analyser, 1, length(analyser) - 2);
            if (substring(analyser, length(analyser), 1) == "1") {
                x_sign = -1.1;
            } else {
                x_sign = 1.8;
            }
        } else {
            element = analyser;
            x_sign = 1.8;
            y_off = -63;
        }
        setnamed("OOSC_" + analyser, "x position", getnamed(element, "x position") + x_sign * 50);
        setnamed("OOSC_" + analyser, "y position", getnamed(element, "y position") + y_off - 100);
        flipelement("OOSC_" + analyser); rotateelement("OOSC_" + analyser);
        setnamed("OSA_" + analyser, "x position", getnamed(element, "x position") + x_sign * 50);
        setnamed("OSA_" + analyser, "y position", getnamed(element, "y position") + y_off - 200);
        flipelement("OSA_" + analyser); rotateelement("OSA_" + analyser);
        setnamed("OPM_" + analyser, "x position", getnamed(element, "x position") + x_sign * 50);
        setnamed("OPM_" + analyser, "y position", getnamed(element, "y position") + y_off - 300);
        flipelement("OPM_" + analyser); rotateelement("OPM_" + analyser);
    }
    
    fixed_down_element = {"W_L_B_1", "W_L_B_2", "W_R_B_1", "W_R_B_2"};    
    down_element = cell(length(fixed_down_element) + (N * 2) + (N - 1) * 2);
    off = 0;
    for (i = 1:length(fixed_down_element)) {
        off = off + 1; down_element{off} = fixed_down_element{i}; 
    }
    for (i = 1:N) {
        off = off + 1; down_element{off} = "R_" + num2str(i) + "_lb";
        off = off + 1; down_element{off} = "R_" + num2str(i) + "_rb";
    }
    for (i = 2:N) {
        off = off + 1; down_element{off} = "W_B_" + num2str(i - 1) + "_" + num2str(i) + "_1";
        off = off + 1; down_element{off} = "W_B_" + num2str(i - 1) + "_" + num2str(i) + "_2";
    }
    for (i = 1:length(down_element)) {
        analyser = down_element{i};
        y_off = 0;
        if (substring(analyser, 1, 2) == "R_") {
            element = substring(analyser, 1, length(analyser) - 3);
            if (substring(analyser, length(analyser) - 1, 1) == "l") {
                x_sign = -1.1;
            } else {
                x_sign = 1.8;
            }
            y_off = 75;
        } else if (substring(analyser, 1, 2) == "W_") {
            element = substring(analyser, 1, length(analyser) - 2);
            if (substring(analyser, length(analyser), 1) == "1") {
                x_sign = -1.1;
            } else {
                x_sign = 1.8;
            }
        } else {
            element = analyser;
            x_sign = 1.8;
            y_off = 63;
        }
        setnamed("OOSC_" + analyser, "x position", getnamed(element, "x position") + x_sign * 50);
        setnamed("OOSC_" + analyser, "y position", getnamed(element, "y position") + y_off + 100);
        rotateelement("OOSC_" + analyser);
        setnamed("OSA_" + analyser, "x position", getnamed(element, "x position") + x_sign * 50);
        setnamed("OSA_" + analyser, "y position", getnamed(element, "y position") + y_off + 200);
        rotateelement("OSA_" + analyser);
        setnamed("OPM_" + analyser, "x position", getnamed(element, "x position") + x_sign * 50);
        setnamed("OPM_" + analyser, "y position", getnamed(element, "y position") + y_off + 300);
        rotateelement("OPM_" + analyser);
    }
    
    # Hiding Analyzers Annotations
    for (i = 1:length(optical_analysers_ports)) {
        setnamed("OOSC_" + optical_analysers_ports{i}, "annotate", annotate_all >= 6);
        setnamed("OSA_" + optical_analysers_ports{i}, "annotate", annotate_all >= 6);
        setnamed("OPM_" + optical_analysers_ports{i}, "annotate", annotate_all >= 6);
    }
}


if (getvariable("main__", "") == currentscriptname) {
    main__ = "";
}
